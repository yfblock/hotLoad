plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'idea'
}

group 'io.github.yfblock'
version '1.2'

ext {
    gitUrl  = 'https://github.com/yfblock/hotLoad'
}

repositories {
    mavenCentral()
}

// 设置多版本兼容
sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
    }
}

dependencies {
    testImplementation 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
}

// 默认编译
compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}

// 多版本之一11编译
compileJava11Java {
    sourceCompatibility '11'
    targetCompatibility '11'
}

// 设置打包为Jar包
jar {
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }

    manifest {
        attributes(
            "Manifest-Version": "1.0",
            "Multi-Release": true
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


test {
    useJUnitPlatform()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            version = rootProject.version
            from components.java
            pom {
                name = rootProject.name
                description = 'hot load'
                packaging = 'jar'
                url = project.ext.gitUrl
                properties = [
                    myProp: "value",
                    "prop.with.dots": "anotherValue"
                ]
                licenses {
                    license {
                        name = 'GPL-2.0'
                        url = 'https://gitee.com/yfblock/hotLoad/raw/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'yufeng'
                        name = 'yufeng'
                        email = '321353225@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:yfblock/hotLoad.git'
                    developerConnection = 'scm:git:ssh://github.com/yfblock/hotLoad.git'
                    url = project.ext.gitUrl
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

idea{
    module{
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}